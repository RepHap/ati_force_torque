##################################################################################################
# 
# CMake script for finding NIDAQmx.
#
# Cache variables (not intended to be used in CMakeLists.txt files)
# 
# - NIDAQmx_INCLUDE_DIR: Absolute path to package headers.
# - NIDAQmx_LIBRARY: Absolute path to library.
# 
# 
# Output variables:
# 
# - NIDAQmx_FOUND: Boolean that indicates if the package was found
# - NIDAQmx_INCLUDE_DIRS: Paths to the necessary header files
# - NIDAQmx_LIBRARIES: Package libraries
# 
# 
# Example usage:
# 
#  find_package(NIDAQmx)
#  if(NOT NIDAQmx_FOUND)
#    # Error handling
#  endif()
#  ...
#  include_directories(${NIDAQmx_INCLUDE_DIRS} ...)
#  ...
#  target_link_libraries(my_target ${NIDAQmx_LIBRARIES})
# 
##################################################################################################

if(WIN32)
  message("WIN32")
  set(NIDAQmx_ROOT_DIR "C:/Program Files (x86)/National Instruments/Shared/ExternalCompilerSupport/C")
  set(NIDAQmx_INCLUDE_PATH "${NIDAQmx_ROOT_DIR}/include")
  if(CMAKE_SIZEOF_VOID_P EQUAL 8)
    set(NIDAQmx_LIBRARY_PATH "${NIDAQmx_ROOT_DIR}/lib64/msvc")
  elseif(CMAKE_SIZEOF_VOID_P EQUAL 4)
    set(NIDAQmx_LIBRARY_PATH "${NIDAQmx_ROOT_DIR}/lib32/msvc")
  else()
    message(FATAL_ERROR "Wrong platform")
  endif()
endif()

# Find headers and libraries
find_path(NIDAQmx_INCLUDE_DIR NAMES NIDAQmx.h PATHS ${NIDAQmx_INCLUDE_PATH})
find_library(NIDAQmx_LIBRARY  NAMES NIDAQmx   PATHS ${NIDAQmx_LIBRARY_PATH})

mark_as_advanced(NIDAQmx_INCLUDE_DIR
                 NIDAQmx_LIBRARY)

# Output variables generation
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(NIDAQmx DEFAULT_MSG NIDAQmx_LIBRARY
                                                      NIDAQmx_INCLUDE_DIR)

set(NIDAQmx_FOUND ${NIDAQMX_FOUND}) # Enforce case-correctness: Set appropriately cased variable...
unset(NIDAQMX_FOUND) # ...and unset uppercase variable generated by find_package_handle_standard_args

if(NIDAQmx_FOUND)
  set(NIDAQmx_INCLUDE_DIRS ${NIDAQmx_INCLUDE_DIR})
  set(NIDAQmx_LIBRARIES ${NIDAQmx_LIBRARY})
endif()
